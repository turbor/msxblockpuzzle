addroutine:	; in DE routine address
	ld hl,routinestack-1
addroutine1:
	inc hl
	ld a,(hl)
	inc hl
	or (hl)
	jr nz,addroutine1
	ld (hl),d
	dec hl
	ld (hl),e
	ret ; assuming next two bytes are still zero!!

delroutine:	; in DE routine to remove
	ld hl,routinestack
delroutine1:
	ld a,e
	cp (hl)
	jr nz,delroutine2
	inc hl
	ld a,d
	cp (hl)
	jr nz,delroutine3
	dec hl
	ld e,l
	ld d,h
	inc hl
	inc hl
delroutine4:
	ld bc,2
	ldir
	;if we did not just copy two zero then move again two bytes
	push de
	push hl

	dec de
	ld a,(de)
	ld l,a
	dec de
	ld a,(de)
	or l
	pop hl
	pop de
	jr nz,delroutine4
	ret

delroutine2:	inc hl
delroutine3:	inc hl
	jr delroutine1



execroutine:
	ld hl,routinestack
execroutine1:
	ld e,(hl)
	inc hl
	ld d,(hl)
	inc hl
	ld a,d
	or e
	ret z
	;else de is a routine to call
	push hl
	ld hl,execroutine3
	push hl
	ex de,hl
	jp (hl)
execroutine3:	pop hl
	jr execroutine1

;	vim:foldmethod=marker:ft=z8a:ts=16
