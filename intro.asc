initintronametabledb:
	db #48,#36,#4B,#46
	db #43,#3D,#39,#39
	db #4D,#36,#50,#47
	db #3D,#3D,#57,#57
	db #4C,#4C,#4C,#4C
	db #4C,#3E,#3E,#49
	INCLUDE "logo.asc"
initintronametable:
	; make the intro screen nametabel, uses char from game itself!
	; {{{ 
	xor a
	ld (intromenuselect),a

	ld hl,nametabel
	ld de,nametabel+1
	ld ix,initintronametabledb
	ld b,24
initintronametable1:
	push bc
	ld a,(ix+0)
	inc ix
	ld (hl),a
	ld bc,32
	ldir
	pop bc
	djnz initintronametable1
	; now copy logo to correct place
	ld hl,nametabel+8
	ld b,8
initintronametable2:
	push bc
	ld b,17
initintronametable3:
	ld a,(ix+0)
	inc ix
	or a
	jr z,initintronametable4
	ld (hl),a
initintronametable4:
	inc hl
	djnz initintronametable3
	ld de,32-17
	add hl,de
	pop bc
	djnz initintronametable2
	ld hl,initintronameplace
initintronametable5:
	ld e,(hl)
	inc hl
	ld d,(hl)
	ld a,e
	or d
	jr z,initintronametable6
	call initintronameblock
	jr initintronametable5
initintronametable6:
	; first make the scroll big charsset
	; it will place this in the 3 patgentabels so do this first we overwrite later
	call createbigcharset
	ld a,#fc
	ld hl,0x2000+256*8+128*8
	ld bc,128*8
	call FILVRM
	ld a,#f2
	ld hl,0x2000+512*8+128*8
	ld bc,128*8
	call FILVRM
	; add the new 125 and up chars
	ld hl,vrampatgameover
	ld de,141*8
	ld bc,vrampatgameoverend-vrampatgameover
	call LDIRVM	; copy patern table
	ld hl,vrampatgameover
	ld de,125*8
	ld bc,vrampatgameoverend-vrampatgameover
	call LDIRVM	; copy patern table
	ld a,#94
	ld hl,0x2000+125*8
	ld bc,vramcolgameoverend-vramcolgameover
	call FILVRM
	ld a,#95
	ld hl,0x2000+#90*8
	ld bc,vramcolgameoverend-vramcolgameover
	call FILVRM
	; add new characters for the 'start game' and hiscore text
scr1charstart:	equ 88
	ld hl,screen1chardb
	ld de,scr1charstart*8+256*8
	ld bc,128*8-scr1charstart*8
	;ld bc,screen1chardbend-screen1chardb
	;ld bc,75*8
	call LDIRVM
	ld hl,screen1chardb
	ld de,scr1charstart*8+512*8
	ld bc,128*8-scr1charstart*8
	;ld bc,screen1chardbend-screen1chardb
	;ld bc,75*8
	call LDIRVM
	ld a,#F7
	ld hl,0x2000+256*8+scr1charstart*8
	ld bc,128*8-scr1charstart*8
	;ld bc,screen1chardbend-screen1chardb
	call FILVRM
	ld a,#F3
	ld hl,0x2000+512*8+scr1charstart*8
	ld bc,128*8-scr1charstart*8
	;ld bc,screen1chardbend-screen1chardb
	call FILVRM

	ld hl,startgamedb
	ld de,nametabel+32*10+11
	ld bc,10
	ldir
	ld hl,startgamedb2
	ld de,nametabel+32*12+11
	ld bc,10
	ldir

	; show current hi-score
	ld hl,hiscore+1
	ld de,#1800+32*23+11 ; vram destination
	ld ix,nametabel+32*23+11
	ld bc,256*scr1charstart+#49 ; offset for numbers in b, empty char in c
	call shownumbers

	; prepare scroller
	ld de,30
	ld (scrollbuflength),de
	ld de,nametabel+14*32
	ld (scrollbufpnt),de
	ld de,scr1charstart
	ld (bigscrolcharspnt),de
	ld de,introbigcharconvert
	ld (bigchartosimpelcnvpnt),de
	ld de,bigscrolchr2vram2tabels
	ld (bigscrolchr2vrampnt),de
	call savefgblocks
	ld de,switchfgblocks
	;ld de,showgameoverret
	ld (bigscrolswitchfgpnt),de
	xor a
	ld (scrollpage),a
	ld hl,scrollpatternendbuf
	ld de,scrollpatternendbuf+1
	ld bc,15
	ld (hl),a
	ldir
	dec a
	ld (scrollpatterncombinedb),a
	xor a
	ld (scrolltextcount),a
	ld a,2
	ld (scrolltextmaxcount),a

	; now show intro screen on screen
	ld hl,nametabel
	ld de,#1800
	ld bc,32*24
	call LDIRVM 

	; small sprites but 16x16
	ld a,(RG1SAV)
	and 252
	or 2
	ld (RG1SAV),a
	ld b,a
	ld c,1
	call WRTVDP

	ld a,80
	call introhandcode6

	ld de,20*256+200
	ld bc,sprbirdoffset
	call setbird
	ret

introbigcharconvert:
	; 0 is empty, 1 is completely full
	db  0, 0, 0, 0
	db  0, 2, 3, 0
	db  0, 5, 6, 0
	db  0, 0, 0, 0
	db  1, 1, 1, 1
	db  1,15,16, 1
	db  1,18,19, 1
	db  1, 1, 1, 1

startgamedb:
	db "D"-#30+scr1charstart-7
	db "I"-#30+scr1charstart-7
	db "F"-#30+scr1charstart-7
	db "F"-#30+scr1charstart-7
	db "I"-#30+scr1charstart-7
	db "C"-#30+scr1charstart-7
	db "U"-#30+scr1charstart-7
	db "L"-#30+scr1charstart-7
	db "T"-#30+scr1charstart-7
	db "Y"-#30+scr1charstart-7

startgamedb2:
	db "S"-#30+scr1charstart-7
	db "T"-#30+scr1charstart-7
	db "A"-#30+scr1charstart-7
	db "R"-#30+scr1charstart-7
	db "T"-#30+scr1charstart-7
	db " "-#30+scr1charstart-7
	db "G"-#30+scr1charstart-7
	db "A"-#30+scr1charstart-7
	db "M"-#30+scr1charstart-7
	db "E"-#30+scr1charstart-7

initintronameplace:
	dw nametabel+32*13+29
	dw nametabel+32*13+31
	dw nametabel+32*15+30
	dw nametabel+32*17+31
	dw nametabel+32*17+29
	dw nametabel+32*17+27
	dw nametabel+32*19+30
	dw nametabel+32*19+26
	dw nametabel+32*19+24
	dw nametabel+32*20+0
	dw nametabel+32*20+2
	dw nametabel+32*20+19
	dw nametabel+32*20+23
	dw nametabel+32*20+25
	dw nametabel+32*22+1
	dw nametabel+32*22+3
	dw nametabel+32*22+29
	dw 0
initintronameblock:
	push de
	pop ix
	ld a,12
	ld (ix+0),a
	inc a
	ld (ix+1),a
	inc a
	ld (ix+32),a
	inc a
	ld (ix+33),a
	ret
	; }}}

introhandcodeinit:
	ld de,introhandcode
	call addroutine
	ret

introhandcode:
	call getjoystick
	ld b,a
	ld a,(prevjoystick)
	ld c,a
	ld a,b
	ld (prevjoystick),a
	xor c
	and c
	bit 0,a
	jr z,introhandcode2
	xor a
	ld (intromenuselect),a
	ld a,80
	jr introhandcode6
introhandcode2:
	bit 1,a
	jr z,introhandcode3
	ld a,1
	ld (intromenuselect),a
	ld a,96
	jr introhandcode6
introhandcode3:
	bit 4,a
	ret z
	;fire pressed once
	ld a,(intromenuselect)
	or a
	jp nz,introhandcode5
	; change hardmode selected
	ld a,(introdifficultselected)
	inc a
	cp 3
	jr c,introhandcode4
	xor a
introhandcode4:
	ld (introdifficultselected),a
	; now display text
	add a,a
	ld e,a
	ld d,0
	ld hl,hardmodetextpnt
	add  hl,de
	ld e,(hl)
	inc hl
	ld d,(hl)
	ex de,hl
	ld de,nametabel+32*10+11
	ld bc,10
	ldir
	ld hl,nametabel+32*10
	ld de,#1800+32*10
	ld bc,32
	jp LDIRVM ; jp instead of calll+ret

hardmodetextpnt	dw hardmodetext1
	dw hardmodetext2
	dw hardmodetext3
hardmodetext1
	db "D"-#30+scr1charstart-7
	db "I"-#30+scr1charstart-7
	db "F"-#30+scr1charstart-7
	db " "-#30+scr1charstart-7
	db " "-#30+scr1charstart-7
	db " "-#30+scr1charstart-7
	db "E"-#30+scr1charstart-7
	db "A"-#30+scr1charstart-7
	db "S"-#30+scr1charstart-7
	db "Y"-#30+scr1charstart-7
hardmodetext2
	db "D"-#30+scr1charstart-7
	db "I"-#30+scr1charstart-7
	db "F"-#30+scr1charstart-7
	db " "-#30+scr1charstart-7
	db "N"-#30+scr1charstart-7
	db "O"-#30+scr1charstart-7
	db "R"-#30+scr1charstart-7
	db "M"-#30+scr1charstart-7
	db "A"-#30+scr1charstart-7
	db "L"-#30+scr1charstart-7
hardmodetext3
	db "D"-#30+scr1charstart-7
	db "I"-#30+scr1charstart-7
	db "F"-#30+scr1charstart-7
	db " "-#30+scr1charstart-7
	db " "-#30+scr1charstart-7
	db " "-#30+scr1charstart-7
	db "H"-#30+scr1charstart-7
	db "A"-#30+scr1charstart-7
	db "R"-#30+scr1charstart-7
	db "D"-#30+scr1charstart-7


introhandcode5:
	; start game selected
	ld a,255
	ld (intromenuselect),a
	ld (waitinterrupted),a ; stop scrolling is start of game
	ret
	

introhandcode6:
	; hand code is spriteattr 3 (0,1,2used for letters)
	ld (spriteatrtabel+4*3),a
	ld (spriteatrtabel+4*4),a
	ld (spriteatrtabel+4*5),a
	ld a,60
	ld (spriteatrtabel+4*3+1),a
	ld (spriteatrtabel+4*4+1),a
	ld (spriteatrtabel+4*5+1),a
	ld a,192
	ld (spriteatrtabel+4*3+2),a
	ld a,196
	ld (spriteatrtabel+4*4+2),a
	ld a,200
	ld (spriteatrtabel+4*5+2),a
	ld a,1
	ld (spriteatrtabel+4*3+3),a
	ld a,14
	ld (spriteatrtabel+4*4+3),a
	ld a,15
	ld (spriteatrtabel+4*5+3),a
	ld de,#1B00
	ld bc,32*4
	ld hl,spriteatrtabel
	call LDIRVM
	ret

;	vim:foldmethod=marker:ft=z8a:ts=16
