; MSX Block Puzzle game, released as entry for the MSXDev'20
; Created by David Heremans, aka turbor on mrc
;
; This code alters the piecerandomdistribution by replacing (at random) one of the easier pieces with one of the harder pieces
;
makepiecesharder:
	; newborn,infant and toddler do not get more difficult over time
	ld a,(introdifficultselected)
	cp 3
	ret c
	; first try for a while to pick a piece to make harder...
	ld b,60
	ld h,piecerandomdistribution/256
makepiecesharder2:
	call RANDOM
	ld l,a
	ld a,(hl)
	or a
	jr z,makepiecesharder3
	cp 6
	jr z,makepiecesharder3
	cp 14
	jr z,makepiecesharder3
	cp 18
	jr nz,makepiecesharder4
makepiecesharder3:
	djnz makepiecesharder2
	ret ; after several tries we give simply give up...

makepiecesharder4:
	; now hl is the adress and a is the piece that needs to be replaced by a harder one
	push hl
	ld b,0
	ld hl,piecorderofhardness
makepiecesharder5:
	cp (hl)
	jr z,makepiecesharder6
	inc b
	inc hl
	jr makepiecesharder5
makepiecesharder6:
	ld a,(introdifficultselected)
	cp 5
	jp z,makepiecesharderramphard
	;otherwise simply incease to the next piece instead of jumping several pieces
	inc hl
	jr makepiecesharder9

makepiecesharderramphard:
	; now b is the number of lower pieces so hl+(18-b) is hardest piece to be replaced with
	ld a,18
	sub b
	ld b,a
	call RANDOM
makepiecesharder7:
	cp b
	jr c,makepiecesharder8
	sub b
	jr makepiecesharder7
makepiecesharder8:
	ld d,0
	ld e,a
	add hl,de
makepiecesharder9:
	ld a,(hl) ; this is now the harder piece
	pop hl
	ld (hl),a
	ret



piecorderofhardness:
	db 0
	db 11,15
	db 1,2,3,4
	db 5
	db 12,16
	db 7,8,9,10
	db 13,17
	db 6
	db 14,18

;	vim:foldmethod=marker:ft=z8a:ts=16
