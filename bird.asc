; MSX Block Puzzle game, released as entry for the MSXDev'20
; Created by David Heremans, aka turbor on mrc
;
; this is the code that handles the bird sprite animation during the intro
;
startbirdanimation:
	ld de,birdinfo
	ld hl,startbirdanimationdb
	ld bc,9
	ldir
	ld de,birdanimation
	jp addroutine
startbirdanimationdb:
	dw #ffff
	dw 1
	dw 20
	dw 0
	db 0


stopbirdanimation:
	ld de,birdanimation
	call delroutine
	ld d,212
	jp setbird

cphlbc:
	ld a,h
	cp b
	ret nz
	ld a,l
	cp c
	ret

birdanimation:
	ld a,(birdinfo+8)
	inc a
	cp 9*2
	jr nz,birdanimation1
	xor a
birdanimation1:
	ld (birdinfo+8),a
	ld hl,(birdinfo+4)
	ld de,(birdinfo+6)
	add hl,de
	ld (birdinfo+4),hl
	ld hl,(birdinfo)
	ld de,(birdinfo+2)
	add hl,de
	ld bc,256+32+7 ; 7 is extra delay
	call cphlbc
	jr nz,birdanimation2
	call RANDOM
	and #38
	ld (birdinfo+4),a
	ld hl,#FFD0
birdanimation2:
	ld (birdinfo),hl

	; now do some EC x-coordinate trickery
	ld a,(birdinfo+4)
	ld d,a ; first d is Y if visible
	; if hl is greater then 255+32 then not visible
	ld bc,256+32
	call cphlbc
	jr nc,birdanimation4
	; ok visible so check if EC is needed
	ld bc,32
	or a
	sbc hl,bc
	ld a,255
	cp h
	jr z,birdanimation3 ; in left border
	ld b,0
	ld e,l
	jr birdanimation5

birdanimation3:
	add hl,bc
	ld b,128
	ld e,l
	jr birdanimation5

birdanimation4:
	ld d,212 ; sprite not visible
	; other parameters are irrelevant
birdanimation5:
	ld a,(birdinfo+8)
	and #fe
	;add a,a
	add a,a
	ld l,a
	add a,a
	add a,l
	add a,sprbirdoffset
	ld c,a


setbird:	; in d=y e=x c=psitechar b=EC bit
	; bird code is spriteattr 6 (0,1,2used for letters, 3,4,5 for hand  selection)
	ld a,d
	ld (spriteatrtabel+4*6),a
	ld (spriteatrtabel+4*7),a
	ld (spriteatrtabel+4*8),a
	ld a,e
	ld (spriteatrtabel+4*6+1),a
	ld (spriteatrtabel+4*7+1),a
	ld (spriteatrtabel+4*8+1),a
	ld a,c
	ld (spriteatrtabel+4*6+2),a
	ld c,4
	add a,c
	ld (spriteatrtabel+4*7+2),a
	add a,c
	ld (spriteatrtabel+4*8+2),a
	ld a,1
	or b
	ld (spriteatrtabel+4*6+3),a
	ld a,14
	or b
	ld (spriteatrtabel+4*7+3),a
	ld a,15
	or b
	ld (spriteatrtabel+4*8+3),a
	jp transferspriteatrtabel
	;ret
;	vim:foldmethod=marker:ft=z8a:ts=16
