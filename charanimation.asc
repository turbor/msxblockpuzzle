; MSX Block Puzzle game, released as entry for the MSXDev'20
; Created by David Heremans, aka turbor on mrc
;
; animate the grid in the 2 piece-preview boxes when new pieces become available
;
charanimationinit:
	ld de,charanimationinit
	call delroutine
	ld de,docharanimation
	call addroutine
	ld a,1
	ld (gridanimationinfo),a ; speed of animation
	ld a,8*4
	ld (gridanimationinfo+1),a ; number of animationframes left
	ld de,gridanimationinfo+2
	ld hl,8*#30+vrampat1
	ld bc,8
	ldir
	ret

docharanimation:
	ld a,(gridanimationinfo)
	dec a
	ld (gridanimationinfo),a
	ret nz
	ld a,3
	ld (gridanimationinfo),a
	ld a,(gridanimationinfo+1)
	dec a
	ld (gridanimationinfo+1),a
	jr nz,docharanimation2
	ld de,docharanimation
	call delroutine
docharanimation2:
	;ld de,gridanimationinfo+2
	;ld hl,gridanimationinfo+2+1
	;ld bc,7
	;ld a,(de)
	;ldir
	;ld (de),a
	ld de,gridanimationinfo+2+6+1
	ld hl,gridanimationinfo+2+6
	ld bc,7
	ld a,(de)
	lddr
	ld (de),a
	ld hl,gridanimationinfo+2
	ld b,8
docharanimation3:
	rlc (hl)
	inc hl
	djnz docharanimation3
	ld de,#30*8
	ld hl,gridanimationinfo+2
	ld bc,8
	call LDIRVM	; copy patern table
	ld de,#30*8+256*8
	ld hl,gridanimationinfo+2
	ld bc,8
	call LDIRVM	; copy patern table
	ld de,#30*8+512*8
	ld hl,gridanimationinfo+2
	ld bc,8
	jp LDIRVM	; copy patern table

;	vim:foldmethod=marker:ft=z8a:ts=16
